plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
}

def readVersionName() {
    def f = new File(rootDir, "version.txt")
    return f.exists() ? f.getText("UTF-8").trim() : "1.0"
}

def computeVersionCode(String ver) {
    def parts = ver.split('\\.')
    def major = parts.length > 0 ? parts[0].toInteger() : 0
    def minor = parts.length > 1 ? parts[1].toInteger() : 0
    def patch = parts.length > 2 ? parts[2].toInteger() : 0
    // versionCode = MMmmpp (e.g., 1.0.4 -> 10004)
    return major * 10000 + minor * 100 + patch
}

android {
    namespace "ir.vinor.app"
    compileSdk 34

    defaultConfig {
        applicationId "ir.vinor.app"
        minSdk 21
        targetSdk 34
        versionName readVersionName()
        versionCode computeVersionCode(versionName)
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile("proguard-android-optimize.txt"), "proguard-rules.pro"
        }
        debug {
            minifyEnabled false
            applicationIdSuffix ".dev"
            versionNameSuffix "-dev"
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    kotlinOptions {
        jvmTarget = "17"
    }
    buildFeatures {
        viewBinding true
    }
}

androidComponents {
    onVariants(selector().all()) { variant ->
        variant.outputs.each { output ->
            output.outputFileName.set("vinor-${readVersionName()}.apk")
        }
    }
}

dependencies {
    implementation "androidx.core:core-ktx:1.13.1"
    implementation "androidx.appcompat:appcompat:1.7.0"
    implementation "com.google.android.gms:play-services-auth-api-phone:18.0.1"
    implementation "com.squareup.okhttp3:okhttp:4.12.0"
    implementation "com.squareup.okhttp3:okhttp-brotli:4.12.0"
}

